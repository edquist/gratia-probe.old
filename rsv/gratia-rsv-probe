eval '(exit $?0)' && eval 'exec perl -S $0 ${1+"$@"}'
  && eval 'exec perl -S $0 $argv:q'
  if 0;
########################################################################
# $Id$
#
# Check if Gratia probe installation is operating nominally.
#
# Based on gridftp-simple-probe v1.15
########################################################################

use strict;
use RSVProbeBase;
use File::Basename;
use Date::Manip;


######## Retrieve Global variables ################################
## And alias to RSV::Probe_Base variables
our %o;         *o         = \%RSVProbeBase::o;
our %metric;    *metric    = \%RSVProbeBase::metric;
my ($is_root, $run_me_root, $probeconfig_problems, $soaphost, $probename,
    $sitename, $enabled, $datafolder, $workingfolder, $real_datafolder,
    $probe_in_cron, $current_gratia_probe_type);

&RSVProbeBase::Init();
&RSVProbeBase::Run();

################################################################################
## Main Program ends ##
################################################################################


################################################################################
## Local Sub-Routines
################################################################################

sub Init {

  &RSVProbeBase::Set_MultiMetric ("true");

  ## &RSVProbeBase::Set_MetricName ("net.sf.gratia.probe.none");

  ## Pass "status" or "performance" depending on metric type
  &RSVProbeBase::Set_MetricType ("status");
  &RSVProbeBase::Set_ServiceType ("OSG-CE");
  &RSVProbeBase::Set_ServiceVersion (">= OSG CE 1.0.0");
  &RSVProbeBase::Set_ProbeType ("OSG-CE");

  ## Define this value from RCS/SVN version
  &RSVProbeBase::Set_ProbeRevision ('3079'); ## ('$Revision$');
  &RSVProbeBase::Set_ProbeSpecVersion ("0.91");

  ## Basic intro for the probe; This string will be used by Print_Usage ()
  &RSVProbeBase::Set_ProbeHelpIntro 
    ("Probe to check whether Gratia probes are configured and operating. ");

  ## Additional options to print in Usage info used by Print_Usage ()
  ## &RSVProbeBase::Set_ProbeHelpOptions ("-p,--probe-type <condor|pbs|sge..>    Required option. Probe expected in $VDT_LOCATION/gratia/probe/<probe-type>");

  ## Uncomment if you want additional command line options
  ## &RSVProbeBase::Extra_CLI_Option ("probe-type|p=s","probeType");

  ## Uncomment if you do not want detailsData to be trimmed to $o{'detailsDataMaxLength}
  &RSVProbeBase::Set_DetailsDataTrim ("False");
}


sub Run {

  &Process_Multi_Metrics ();

  $is_root = not $<;		# Are we running as root?

  ####################################
  # Start tests
  my @PYTHONPATH = split /:/, $ENV{PYTHONPATH};
  unless (grep m&^\Q$ENV{VDT_LOCATION}/gratia/probe/common\E$&, @PYTHONPATH) {
    push @PYTHONPATH, "$ENV{VDT_LOCATION}/gratia/probe/common";
    $ENV{PYTHONPATH} = join(":", @PYTHONPATH);
  }

  $current_gratia_probe_type = gratiaProbeType();

  # Check directories
  unless (checkProbeDirs()) {
    setUnknown();
    normal_exit();
  }

  $soaphost = getProbeConfigAttribute("SOAPHost");
  $probename = getProbeConfigAttribute("MeterName");
  $sitename = getProbeConfigAttribute("SiteName");
  $enabled = getProbeConfigAttribute("EnableProbe");
  $datafolder = getProbeConfigAttribute("DataFolder");
  $datafolder =~ s&/$&&;
  $workingfolder = getProbeConfigAttribute("WorkingFolder");
  $real_datafolder = `(cd "$datafolder" >/dev/null 2>&1 && /bin/pwd)`;
  chomp $real_datafolder;

  $probe_in_cron = system("crontab -l 2>/dev/null| grep -e '^\\s*[^#].*gratia/probe/$current_gratia_probe_type/' >/dev/null 2>&1") == 0;

  ################################
  # Check some basic critical stuff

  # Is probe in crontab?
  if ($probe_in_cron != 1) {
    if ($is_root) {
      setCritical();
      &RSVProbeBase::Append_DetailsData("Probe $probename is not listed in crontab.\n");
    } else {
      setUnknown();
      &RSVProbeBase::Append_DetailsData("Not root: cannot examine crontab.\n");
    }
  }
  # Is probe enabled?
  if ($enabled ne "1") {
    setCritical();
    &RSVProbeBase::Append_DetailsData("Probe $probename has EnableProbe not set to 1.\n");
    $run_me_root = 1;
    $probeconfig_problems = 1;
  }
  # Is probename generic or unset?
  if ($probename =~ m&(?:generic|unknown)&i) {
    setCritical();
    &RSVProbeBase::Append_DetailsData("MeterName $probename appears to be generic.\n");
    $probeconfig_problems = 1;
    normal_exit();
  }

  # Do we have the correct permissions on the datafolder?
  my (@rdf_stat) = stat $real_datafolder;
  if ($rdf_stat[2] != 041777) {
    setCritical();
    &RSVProbeBase::Append_DetailsData(sprintf("%s (found %05o, wanted %05o).",
                           "Data folder $real_datafolder does not have correct permissions\.n",
                           $rdf_stat[2],
                           041777));
  }

  #################################
  # Check format of MeterName
  my @colon_split = split /:/, $probename;
  my $num_colons = $#colon_split;
  if ($num_colons != 1) {
    setWarning();
    &RSVProbeBase::Append_DetailsData("Not one and only one \`:' in MeterName $probename.\n");
    $probeconfig_problems = 1;
  }

  ################################
  # Check sitename configuration.
  if ((!$sitename) or $sitename =~ m&(?:generic|unknown)&i) {
    setWarning();
    &RSVProbeBase::Append_DetailsData (<<EOF);
Configured SiteName "$sitename" is empty or generic.
EOF
    $probeconfig_problems = 1;
  }

  ################################
  # Any checks specific to the probe type
  probe_specific_checks();

  ################################
  # Check size of reprocessing queue
  my $npending = `(cd "$workingfolder/gratiafiles" >/dev/null 2>&1 && ls -1 | wc -l)`;
  chomp $npending;
  &RSVProbeBase::Append_DetailsData ("$npending files waiting to be re-sent to $soaphost.\n");
  if ($npending > 1000) {
    setWarning();
  }

  ################################
  # Check number of files in data area.
  my $ndata = `(cd "$datafolder" >/dev/null 2>&1 && ls -1 | wc -l)`;
  chomp $ndata;
  &RSVProbeBase::Append_DetailsData ("$ndata files in data folder $datafolder.\n");
  if ($ndata > 10000) {
    setWarning();
  }

  ################################
  # Check last contact time with Gratia server.
  my $probe_contact_check =
    `wget -q -O - 'http://$soaphost/gratia-administration/monitor-status.html?probename=$probename'`;
  chomp $probe_contact_check;

  $probe_contact_check =~ s&^last-contact=([^\s]+)\s+([^\s]+?)\|$&${1}T${2}Z&;
  $probe_contact_check = ParseDate($probe_contact_check);
  my $last_contact_string = UnixDate($probe_contact_check, "%Y-%m-%d %T %Z");
  my $cmpDateHour = DateCalc("today", "- 1 hour");
  my $cmpDateDay = DateCalc("today", "- 1 day");
  my $probe_seen_by_soaphost;
  if (!$probe_contact_check) {
    setUnknown();
    &RSVProbeBase::Append_DetailsData (<<EOF);
Gratia server $soaphost not responding to obtain last contact details.
EOF
  } elsif ($probe_contact_check eq "last-contact=never") {
    setCritical();
    $run_me_root =1;
    &RSVProbeBase::Append_DetailsData (<<EOF);
Gratia server $soaphost has never seen probe $probename.
EOF
    normal_exit();
  } elsif (Date_Cmp($probe_contact_check, $cmpDateDay) < 1) {
    setCritical();
    $run_me_root =1;
    &RSVProbeBase::Append_DetailsData (<<EOF);
Probe $probename has not been seen by Gratia collector $soaphost in >1d.
Last contact time = $last_contact_string
EOF
    normal_exit();
  } elsif (Date_Cmp($probe_contact_check, $cmpDateHour) < 1) {
    setWarning();
    $run_me_root =1;
    &RSVProbeBase::Append_DetailsData (<<EOF);
Probe $probename has not been seen by Gratia collector $soaphost in >1h.
Last contact time = $last_contact_string
EOF
  } else {
    $probe_seen_by_soaphost = 1;
  }

  ################################
  # Cross-check locally configured site name matches Gratia server sitename.
  if ($probe_seen_by_soaphost) {
    my $probe_site_check =
      system("wget -q -O - 'http://$soaphost/gratia-administration/monitor-status.html?sitename=$sitename' 2>/dev/null | grep -e '^probename=$probename|last-contact=' >/dev/null 2>&1") == 0;

    if (!$probe_site_check) {
      setWarning();
      &RSVProbeBase::Append_DetailsData (<<EOF);
Probe $probename not found in check of probes reporting to $soaphost from $sitename.
Check Collector SiteName translation on $soaphost
EOF
    }
  }

  ################################
  # Check log file for jobs reported.

  # Set final status if we make it.
  &RSVProbeBase::Set_Summary_Metric_Results (0, "") if ($metric{metricStatus} eq "UNKNOWN");

  normal_exit(1);
}


sub runMeRoot {
  &RSVProbeBase::Append_DetailsData (<<EOF);
Run tests as root for more diagnostic details, especially if you have a
WARNING or CRITICAL status.
Manual invocation:
EOF
  my $dir=dirname($0);
  if ($dir eq ".") {
    $dir=`/bin/pwd`;
    chomp $dir;
  }
  &RSVProbeBase::Append_DetailsData (sprintf("cd %s && ./%s -p %s\n",
                                             $dir,
                                             basename($0),
                                             $current_gratia_probe_type));
}

sub probeConfigProblems {
  &RSVProbeBase::Append_DetailsData (<<EOF);
Please check ProbeConfig for problems.
EOF
}

sub normal_exit {
  return if $o{test} and not $_[0];
  probeConfigProblems() if $probeconfig_problems;
  runMeRoot() if ($run_me_root and not $is_root);
  ## Print metric, and we're all done!
  &RSVProbeBase::Print_Metric ();
}


sub setCritical {
  &RSVProbeBase::Set_MetricStatus ("CRITICAL");
}

sub setWarning {
  &RSVProbeBase::Set_MetricStatus ("WARNING") unless
    &RSVProbeBase::Get_MetricStatus () eq "CRITICAL";
}

sub setUnknown {
  &RSVProbeBase::Set_MetricStatus ("UNKNOWN") unless
    &RSVProbeBase::Get_MetricStatus () eq "CRITICAL";
}

sub probe_specific_checks {
  if ($current_gratia_probe_type =~ m&(?:pbs|pbs-lsf|lsf)&i) {
    # PBS / LSF probe checks
    probe_check_pbs_lsf();
  } elsif ($current_gratia_probe_type eq "condor") {
    probe_check_condor();
  }
}

sub probe_check_condor() {
  my $get_condor_config_cmd = ". $ENV{VDT_LOCATION}/setup.sh; condor_config_val PER_JOB_HISTORY_DIR";
  my $cmd_output = `$get_condor_config_cmd 2>&1`;
  my $status = $?;
  if ($status == 0) {
    # Make sure we're not suffering from alias problems.
    chomp $cmd_output;
    $cmd_output = `(cd "$cmd_output" >/dev/null 2>&1 && /bin/pwd)`
      if $cmd_output;
    chomp $cmd_output;
  }
  if ($status == 127 or $cmd_output =~ m&command not found&) {
    setUknown();
    &RSVProbeBase::Append_DetailsData (<<EOF);
Unable to find condor_config_val command: could not check for presence
of PER_JOB_HISTORY_DIR attribute in condor schedd configuration.
$cmd_output
Please check manually.
EOF
    return 0;
  } elsif ($cmd_output =~ m&^Not defined:&i or $status != 0 or not $cmd_output) {
    setWarning();
    &RSVProbeBase::Append_DetailsData (<<EOF);
PER_JOB_HISTORY_DIR apparently not set in condor_config or condor_config_val
not operating as expected. Please ensure it is set to "$datafolder" in
condor_config or the schedd machine's local configuration.
EOF
    return 0;
  } else {
    if ($status == 0 and $cmd_output ne $datafolder) {
      setWarning();
      &RSVProbeBase::Append_DetailsData (<<EOF);
PER_JOB_HISTORY_DIR is apparently set to "$cmd_output"
in condor_config. It should be set to
"$datafolder" as specified in ProbeConfig.
Please rectify in condor_config or the schedd machine's local configuration.
EOF
      return 0;
    }
  }
  return 1;
}

sub probe_check_pbs_lsf {
  my $URCOLLECTOR_LOC = probeDirPath();
  push @INC, $URCOLLECTOR_LOC;
  eval "require urCollector::Configuration";
  if ($@) {                     # FAILED to load configuration module
    setCritical();
    &RSVProbeBase::Append_DetailsData (<<EOF);
Error loading urCollector::Configuration: $@
Major probe installation problem.
EOF
    normal_exit();
  }
  eval
    urCollector::Configuration::parseConf("$URCOLLECTOR_LOC/urCollector.conf");
  if ($@) {                     # FAILED to parse configuration
    setCritical();
    &RSVProbeBase::Append_DetailsData (<<EOF);
Error reading $URCOLLECTOR_LOC/urCollector.conf: $@
Please check urCollector.conf file for errors.
EOF
    normal_exit();
  }
  # Check LRMS type setting
  if ($urCollector::Configuration::configValues{lrmsType} !~ m&(?:pbs|lsf)&i) {
    setCritical();
    &RSVProbeBase::Append_DetailsData (<<EOF);
Unrecognized lrmsType $urCollector::Configuration::configValues{lrmsType} in urCollector.conf
Please check urCollector.conf file for errors.
EOF
    normal_exit();
  }
  # Check LRMS type matches probe type
  if ($current_gratia_probe_type ne "pbs-lsf" and
      $urCollector::Configuration::configValues{lrmsType} ne $current_gratia_probe_type) {
    setCritical();
    &RSVProbeBase::Append_DetailsData (<<EOF);
Mismatch between probeType $current_gratia_probe_type and configured lrmsType $urCollector::Configuration::configValues{lrmsType} in urCollector.conf.
Please check urCollector.conf file for errors.
EOF
    normal_exit();
  }
  unless ($is_root) {		# Can't do these unless we're root.
    $run_me_root = 1;
    &RSVProbeBase::Append_DetailsData (<<EOF);
Remaining checks on $urCollector::Configuration::configValues{lrmsType} require root privilege.
EOF
    return;
  }
  # Check log directory
  if ($urCollector::Configuration::configValues{lrmsType} eq "pbs") {
    my $dir = $urCollector::Configuration::configValues{pbsAcctLogDir};
    my $date = UnixDate("today", "%Y%m%d");
    if (not -d $dir) {
      setCritical();
      &RSVProbeBase::Append_DetailsData (<<EOF);
Configured PBS log directory $dir does not exist.
Check pbsAcctLogDir in urCollector.conf.
EOF
      normal_exit();
    }
    my $nfiles = `ls -1 $dir | wc -l`;
    chomp $nfiles;
    if (!$nfiles) {
      setCritical();
      &RSVProbeBase::Append_DetailsData (<<EOF);
No files found in configured PBS log directory $dir.
Check pbsAcctLogDir in urCollector.conf.
EOF
      normal_exit();
    } elsif ((not -e "$dir/$date") and (not -e "$dir/$date.gz")) {
      setWarning();
      &RSVProbeBase::Append_DetailsData (<<EOF);
Configured PBS log directory $dir does not contain entries for today.
Check pbsAcctLogDir in urCollector.conf and contents of $dir.
EOF
    }
  } elsif ($urCollector::Configuration::configValues{lrmsType} eq "lsf") {
    my $dir = $urCollector::Configuration::configValues{lsfAcctLogDir};
    if (not -d $dir) {
      setCritical();
      &RSVProbeBase::Append_DetailsData (<<EOF);
Configured LSF log directory $dir does not exist.
Check lsfAcctLogDir in urCollector.conf.
EOF
      normal_exit();
    } elsif (not -e "$dir/lsb.events") {
      setWarning();
      &RSVProbeBase::Append_DetailsData (<<EOF);
No log file $dir/lsb.events.
Check lsfAcctLogDir in urCollector.conf and contents of $dir.
EOF
    }
    # Check PBS files waiting to be converted to Gratia files
    my $npending = `(cd "$workingfolder/urCollector" >/dev/null 2>&1 && ls -1 | wc -l)`;
    chomp $npending;
    &RSVProbeBase::Append_DetailsData ("$npending XML files waiting to be sent to $soaphost\n");
    if ($npending > 2000) {
      setWarning();
    }
  }
}

sub checkProbeDirs {
  my $probe_dir = probeDirPath();
  return (checkDirReadable($probe_dir, "$current_gratia_probe_type probe directory") and
          checkFileReadable("$probe_dir/ProbeConfig"));
}

sub checkDirReadable {
  my ($dir, $description) = @_;
  if (-d $dir and -r $dir) {
    return 1;
  } else {
    &RSVProbeBase::Append_DetailsData (<<EOF);
DirectoryCheck: $dir ($description) does not exist or is not readable.
EOF
    if (not $is_root) {
      &RSVProbeBase::Append_DetailsData (<<EOF);
DirectoryCheck: Re-run as root?
EOF
    }
    return 0;
  }
}

sub checkFileReadable {
  my ($file, $description) = @_;
  if (-f $file and -r $file) {
    return 1;
  } else {
    &RSVProbeBase::Append_DetailsData (<<EOF);
FileCheck: $file ($description) does not exist or is not readable.
EOF
    if (not $is_root) {
      &RSVProbeBase::Append_DetailsData (<<EOF);
FileCheck: Re-run as root?
EOF
    }
    return 0;
  }
}

sub getProbeConfigAttribute {
  my $probeDirPath = probeDirPath();
  my $probeConfig_cmd = <<EOF;
( cd "$probeDirPath"; ../common/GetProbeConfigAttribute.py %s )
EOF

  my @results = ();

  foreach my $configAttribute (@_) {
    my $cmd = sprintf($probeConfig_cmd, $configAttribute);
    push @results, `$cmd 2>/dev/null`;
  }

  chomp @results;
  if (wantarray) {
    return @results;
  } else {
    return $results[0];
  }
}

sub gratiaProbeType {
  return $o{'gratiaProbeTypes'}{&RSVProbeBase::Get_MetricName()} || "UNKNOWN";
}

sub probeDirPath {
  return "$ENV{VDT_LOCATION}/gratia/probe/$current_gratia_probe_type";
}

################################################################################
##
##  Process_Informational_Arguments ()
##
## OUTPUT:
##  None
##
## RETURNS:
##  None
## 
################################################################################

sub Process_Multi_Metrics {

  &discoverProbesToTest();

  ## Additional details for this multi-metric probe
  ##$o{'gratiaProbeTypes'}{'org.osg.batch.jobmanager-default-status'} = "jobmanager";

  ## Do above step for each metric possible
  if (defined ($o{'listmetric'})) {
    foreach my $metric_name (sort keys %{$o{'gratiaProbeTypes'}}) {
      &RSVProbeBase::Set_MetricName($metric_name);
      ## Should automating tools run this probe on above probeType by default?
      #if ($metric_name =~ /default/) {
      &RSVProbeBase::Set_EnableByDefault("true");
      #    }

      ## Unix cron type metric interval
      &RSVProbeBase::Set_MetricInterval (int(rand(60)). " * * * *");
      &RSVProbeBase::List_Summary_Metric();
    }
    exit 0;
  }
}

sub discoverProbesToTest {
  my $prog_name = basename $0;

  unless ($ENV{VDT_LOCATION}) {
    print STDERR "$prog_name: VDT_LOCATION is not set!\n";
    exit(1);
  }
  my $probedir = "$ENV{VDT_LOCATION}/gratia/probe";

  unless (-d $probedir) {
    print STDERR "$prog_name: no directory $ENV{VDT_LOCATION}/gratia/probe!\n";
    exit(1);
    }

    unless (opendir(DIR, $probedir)) {
      print STDERR "$prog_name: unable to open directory for $ENV{VDT_LOCATION}/gratia/probe!\n";
      exit(1);
    }

    # Execute probe once per probe directory.
    while (my $dir = readdir DIR) {
      next unless -d "$probedir/$dir";
      next if $dir =~ m&^\.+$&;
      next if $dir =~ m&^(?:rsv|test|common)$&;
      # next if grep m&^\Q$dir\E$&, @excludes;
      $o{'gratiaProbeTypes'}{'org.osg.gratia.'.$dir} = $dir;
      &RSVProbeBase::Verbose ("Assigning probeType to hash \$o{'gratiaProbeTypes'}{'org.osg.gratia.'.$dir}=[$o{'gratiaProbeTypes'}{'org.osg.gratia.'.$dir}]\n");
    }
  closedir(DIR);
}

__END__
### Local Variables:
### mode: cperl
### End:
